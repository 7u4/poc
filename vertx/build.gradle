plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

group = "br.com.leonardoferreira"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

application {
    mainClassName = "io.vertx.core.Launcher"
}

def mainVerticleName = "br.com.leonardoferreira.poc.MainVerticle"
def watchForChange = "src/**/*"
def doOnChange = "./gradlew classes"

dependencies {
    implementation "io.vertx:vertx-web:3.9.1"
    implementation "io.vertx:vertx-web-client:3.9.1"

    implementation "io.vertx:vertx-lang-kotlin:3.9.1"
    implementation "io.vertx:vertx-lang-kotlin-coroutines:3.9.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "12"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "12"
}

shadowJar {
    classifier = "fat"
    manifest {
        attributes "Main-Verticle": mainVerticleName
    }
    mergeServiceFiles {
        include "META-INF/services/io.vertx.core.spi.VerticleFactory"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

run {
    args = ["run", mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}
